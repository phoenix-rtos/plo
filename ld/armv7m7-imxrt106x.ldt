/*
 * Phoenix-RTOS
 *
 * Operating system loader
 *
 * Linker Template and Platform Config for ARMv7 M7 i.MX RT106x
 *
 * Copyright 2021-2022 Phoenix Systems
 * Author: Gerard Swiderski
 *
 * This file is part of Phoenix-RTOS.
 *
 * %LICENSE%
 */


#ifndef ARMV7M7_IMXRT106X_LDT
#define ARMV7M7_IMXRT106X_LDT


/* Platform specific definitions */
#define SIZE_PAGE  0x200
#define SIZE_STACK (4 * SIZE_PAGE)
#define SIZE_HEAP  (8 * SIZE_PAGE)

#if defined(__LINKER__)

/* FlexRAM configuration */
#if defined(CUSTOM_FLEXRAM_CONFIG)
FLEXRAM_CONFIG = CUSTOM_FLEXRAM_CONFIG;
FLEXRAM_ITCM_BANKS = CUSTOM_FLEXRAM_ITCM_BANKS;
FLEXRAM_DTCM_BANKS = CUSTOM_FLEXRAM_DTCM_BANKS;
#else
FLEXRAM_CONFIG = 0xaaaaabff;
FLEXRAM_ITCM_BANKS = 5;
FLEXRAM_DTCM_BANKS = 11;
#endif

FLEXRAM_ITCM_AREA = FLEXRAM_ITCM_BANKS * 32k;
FLEXRAM_ITEXT_AREA = 14 * SIZE_PAGE;
FLEXRAM_ITEXT_ADDR = FLEXRAM_ITCM_AREA - FLEXRAM_ITEXT_AREA;
FLEXRAM_DTCM_AREA = FLEXRAM_DTCM_BANKS * 32k;

/* Memory map setup */
MEMORY
{
	/* TODO: use FLEXRAM_CONFIG value to setup ocram/itcm/dtcm partitioning (*32k) */
	m_itcm    (rwx) : ORIGIN = 0x00000000, LENGTH = FLEXRAM_ITCM_AREA
	m_itext   (rwx) : ORIGIN = FLEXRAM_ITEXT_ADDR, LENGTH = FLEXRAM_ITEXT_AREA
	m_dtcm    (rw)  : ORIGIN = 0x20002000, LENGTH = FLEXRAM_DTCM_AREA
	m_ocram   (rwx) : ORIGIN = 0x20200000, LENGTH = 0 * 32k
	m_flash   (rx)  : ORIGIN = 0x70000000, LENGTH = 4M
}

#if defined (ram)

/* RAM image */
REGION_ALIAS("PLO_IMAGE", m_itcm);
REGION_ALIAS("DATA", m_itcm);

#else

/* FLASH image */
REGION_ALIAS("PLO_IMAGE", m_flash);
REGION_ALIAS("DATA", m_dtcm);

#endif

/* RAM and FLASH image */
REGION_ALIAS("TCM_TEXT", m_itext);
REGION_ALIAS("BSS", m_dtcm);
REGION_ALIAS("HEAP", m_dtcm);
REGION_ALIAS("STACK", m_dtcm);

#include "common/plo-arm.lds"

#endif /* end of __LINKER__ */


#endif /* end of ARMV7M7_IMXRT106X_LDT */
