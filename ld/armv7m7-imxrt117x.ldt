/*
 * Phoenix-RTOS
 *
 * Operating system loader
 *
 * Linker Template and Platform Config for ARMv7 M7 i.MX RT117x
 *
 * Copyright 2021-2023 Phoenix Systems
 * Author: Gerard Swiderski
 *
 * This file is part of Phoenix-RTOS.
 *
 * %LICENSE%
 */


#ifndef ARMV7M7_IMXRT117X_LDT
#define ARMV7M7_IMXRT117X_LDT


/* Platform specific definitions */
#define SIZE_PAGE  0x200
#define SIZE_STACK (8 * SIZE_PAGE)
#define SIZE_HEAP  (8 * SIZE_PAGE)

/* Space reserved for kernel data */
#define AREA_KERNEL 0x2000

/* Space reserved for bootloader */
#define AREA_BOOTLOADER 0x10000


#if defined(__LINKER__)

/* FlexRAM configuration */
#if defined(CUSTOM_FLEXRAM_CONFIG)
FLEXRAM_CONFIG = CUSTOM_FLEXRAM_CONFIG;
FLEXRAM_ITCM_BANKS = CUSTOM_FLEXRAM_ITCM_BANKS;
FLEXRAM_DTCM_BANKS = CUSTOM_FLEXRAM_DTCM_BANKS;
#else
FLEXRAM_CONFIG = 0xaaaaffff;
FLEXRAM_ITCM_BANKS = 8;
FLEXRAM_DTCM_BANKS = 8;
#endif

FLEXRAM_ITCM_AREA = FLEXRAM_ITCM_BANKS * 32k;
FLEXRAM_ITEXT_AREA = 11 * SIZE_PAGE;
FLEXRAM_ITEXT_ADDR = FLEXRAM_ITCM_AREA - FLEXRAM_ITEXT_AREA;
FLEXRAM_DTCM_AREA = FLEXRAM_DTCM_BANKS * 32k;

/* Memory map setup */
MEMORY
{
	m_itcm    (rwx) : ORIGIN = 0x00000000, LENGTH = FLEXRAM_ITCM_AREA
	m_itext   (rwx) : ORIGIN = 0x00000000 + FLEXRAM_ITEXT_ADDR, LENGTH = FLEXRAM_ITEXT_AREA
	m_dtcm    (rw)  : ORIGIN = 0x20002000 + AREA_KERNEL, LENGTH = FLEXRAM_DTCM_AREA - AREA_KERNEL
	m_ocram1  (rwx) : ORIGIN = 0x20240000 + AREA_BOOTLOADER, LENGTH = (8 * 32k) - AREA_BOOTLOADER
	m_ocram2  (rwx) : ORIGIN = 0x202c0000, LENGTH = 512k
	m_flash   (rx)  : ORIGIN = 0x30000000, LENGTH = 4M
}

#if defined (ram)

/* RAM image */
REGION_ALIAS("PLO_IMAGE", m_ocram1);
REGION_ALIAS("DATA", m_ocram1);

#else

/* FLASH image */
REGION_ALIAS("PLO_IMAGE", m_flash);
REGION_ALIAS("DATA", m_dtcm);

#endif

/* RAM and FLASH image */
REGION_ALIAS("TCM_TEXT", m_itext);
REGION_ALIAS("BSS", m_dtcm);
REGION_ALIAS("HEAP", m_dtcm);
REGION_ALIAS("STACK", m_dtcm);

#include "common/plo-arm.lds"

#endif /* end of __LINKER__ */


#endif /* end of ARMV7M7_IMXRT117X_LDT */
