/*
 * Phoenix-RTOS
 *
 * Operating system loader
 *
 * Linker Template and Platform Config for ARMv7 A7 i.MX 6ULL
 *
 * Copyright 2021-2022 Phoenix Systems
 * Author: Gerard Swiderski
 *
 * This file is part of Phoenix-RTOS.
 *
 * %LICENSE%
 */


#ifndef ARMV7A7_IMX6ULL_LDT
#define ARMV7A7_IMX6ULL_LDT


/* Platform specific definitions */
#define SIZE_PAGE  0x1000
#define SIZE_STACK (2 * SIZE_PAGE)
#define SIZE_HEAP  (2 * SIZE_PAGE)

#define ADDR_DDR 0x80000000
#define SIZE_DDR (128 * 1024 * 1024)

#define ADDR_OCRAM_HIGH 0x907000
#define SIZE_OCRAM_HIGH (100 * 1024)

#define ADDR_OCRAM_LOW 0x900000
#define SIZE_OCRAM_LOW (28 * 1024)

/* DDR memory to allocate plo's data */
#define SIZE_DDR_PLO (4 * 1024 * 1024)

#define ADDR_BUFF_DDR (ADDR_DDR + SIZE_DDR - SIZE_DDR_PLO)
#define SIZE_BUFF_DDR (2 * 1024 * 1024)

#define ADDR_UNCACHED_BUFF_DDR (ADDR_BUFF_DDR + SIZE_BUFF_DDR)
#define SIZE_UNCACHED_BUFF_DDR (2 * 1024 * 1024)

/* NAND buffers */
#define SIZE_NAND_DMA       (4 * 1024)        /* DMA chain buffer */
#define SIZE_NAND_BUFF      (9 * 1024)        /* Handle up to 8KB page with extra 1KB for OOB */
#define SIZE_NAND_PAGE      (9 * 1024)        /* Handle up to 8KB page with extra 1KB for OOB */
#define SIZE_NAND_RAW_PAGE  (10 * 1024)        /* Handle up to 8KB page with extra 2KB for OOB and meta */
#define SIZE_NAND_BLOCK     (2 * 1024 * 1024) /* Handle up to 2MB eraseblock */

#define ADDR_BUFF_NAND_DDR (ADDR_UNCACHED_BUFF_DDR + SIZE_UNCACHED_BUFF_DDR)
#define SIZE_BUFF_NAND_DDR (SIZE_NAND_DMA + SIZE_NAND_BUFF + SIZE_NAND_PAGE + SIZE_NAND_RAW_PAGE + SIZE_NAND_BLOCK)


#if defined(__LINKER__)

/* Memory map setup */
MEMORY
{
	m_ocram_high   (rwx) : ORIGIN = ADDR_OCRAM_HIGH, LENGTH = SIZE_OCRAM_HIGH
	m_ocram_low    (rw)  : ORIGIN = ADDR_OCRAM_LOW, LENGTH = SIZE_OCRAM_LOW
	m_ddr   (rw)  : ORIGIN = ADDR_BUFF_DDR, LENGTH = SIZE_BUFF_DDR
	m_uncached_ddr (rw)  : ORIGIN = ADDR_UNCACHED_BUFF_DDR, LENGTH = SIZE_UNCACHED_BUFF_DDR
	m_ddr_nand   (rw)  : ORIGIN = ADDR_BUFF_NAND_DDR, LENGTH = SIZE_BUFF_NAND_DDR
}

REGION_ALIAS("TCM_TEXT", m_ocram_low);
REGION_ALIAS("DATA", m_ocram_low);
REGION_ALIAS("HEAP", m_ocram_low);
REGION_ALIAS("STACK", m_ocram_low);
REGION_ALIAS("PLO_IMAGE", m_ocram_high);
REGION_ALIAS("BSS", m_ocram_high);
REGION_ALIAS("DDR", m_ddr);
REGION_ALIAS("UNCACHED_DDR", m_uncached_ddr);
REGION_ALIAS("NAND", m_ddr_nand);

#include "common/plo-arm.lds"


/* Additional sections */
SECTIONS
{
	.ddr (NOLOAD) :
	{
		. = ALIGN(4);
		__ddr_start = .;
		*(.ddr*)
		__ddr_end = .;
	} > DDR

	.uncached_ddr (NOLOAD) :
	{
		. = ALIGN(4);
		__uncached_ddr_start = .;
		*(.uncached_ddr*)
		__uncached_ddr_end = .;
	} > UNCACHED_DDR

	.nand (NOLOAD) :
	{
		. = ALIGN(4);
		__nand_start = .;
		nand_dma = .;
		. += SIZE_NAND_DMA;
		. = ALIGN(4);
		nand_buff = .;
		. += SIZE_NAND_BUFF;
		. = ALIGN(4);
		nand_page = .;
		. += SIZE_NAND_PAGE;
		. = ALIGN(4);
		nand_raw_page = .;
		. += SIZE_NAND_RAW_PAGE;
		. = ALIGN(4);
		nand_block = .;
		. += SIZE_NAND_BLOCK;
		__nand_end = .;
	} > NAND
}


#endif /* end of __LINKER__ */


#endif /* end of ARMV7A7_IMX6ULL_LDT */
