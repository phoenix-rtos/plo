/*
 * Phoenix-RTOS
 *
 * Operating system loader
 *
 * Linker Template and Platform Config for ARMv7 R5 ZynqMP
 *
 * Copyright 2021-2025 Phoenix Systems
 * Author: Gerard Swiderski, Jacek Maksymowicz
 *
 * This file is part of Phoenix-RTOS.
 *
 * %LICENSE%
 */


#ifndef ARMV7R5F_ZYNQMP_LDT
#define ARMV7R5F_ZYNQMP_LDT


/* Platform specific definitions */
#define SIZE_PAGE  0x1000
#define SIZE_STACK (4 * SIZE_PAGE)
#define SIZE_HEAP  (5 * SIZE_PAGE)

#define ADDR_ATCM  0x00000000
#define SIZE_ATCM  (128 * 1024)

#define ADDR_BTCM  0x00020000
#define SIZE_BTCM  (128 * 1024)

#define ADDR_OCRAM_LOW  0xfffc0000
#define SIZE_OCRAM_LOW  (192 * 1024)

#define ADDR_OCRAM_HIGH 0xffff0000
#define SIZE_OCRAM_HIGH (64 * 1024)

#define ADDR_DDR 0x00100000
#define SIZE_DDR (512 * 1024 * 1024)

#define SIZE_BITSTREAM (20 * 1024 * 1024)
#define ADDR_BITSTREAM (ADDR_DDR + SIZE_DDR - SIZE_BITSTREAM)

/* DDR memory to allocate plo's data */
#define SIZE_DDR_PLO (4 * 1024 * 1024)

#define ADDR_BUFF_DDR (ADDR_DDR + SIZE_DDR - SIZE_DDR_PLO - SIZE_BITSTREAM)
#define SIZE_BUFF_DDR (2 * 1024 * 1024)

#define ADDR_UNCACHED_BUFF_DDR (ADDR_BUFF_DDR + SIZE_BUFF_DDR)
#define SIZE_UNCACHED_BUFF_DDR (2 * 1024 * 1024)

#if defined(__LINKER__)

/* Memory map setup */
MEMORY
{
	m_atcm         (rw)  : ORIGIN = ADDR_ATCM, LENGTH = SIZE_ATCM
	m_btcm         (rw)  : ORIGIN = ADDR_BTCM, LENGTH = SIZE_BTCM
	m_ddr          (rw)  : ORIGIN = ADDR_BUFF_DDR, LENGTH = SIZE_BUFF_DDR
	m_uncached_ddr (rw)  : ORIGIN = ADDR_UNCACHED_BUFF_DDR, LENGTH = SIZE_UNCACHED_BUFF_DDR
	m_ocram_low    (rwx) : ORIGIN = ADDR_OCRAM_LOW, LENGTH = SIZE_OCRAM_LOW
	m_ocram_high   (rwx) : ORIGIN = ADDR_OCRAM_HIGH, LENGTH = SIZE_OCRAM_HIGH
}

REGION_ALIAS("INIT_VECTORS", m_ocram_high);
REGION_ALIAS("PLO_IMAGE", m_ocram_low);
REGION_ALIAS("TCM_TEXT", m_ocram_low);
REGION_ALIAS("DATA", m_ocram_low);
REGION_ALIAS("BSS", m_ocram_low);
REGION_ALIAS("HEAP", m_ocram_low);
REGION_ALIAS("STACK", m_ocram_low);
REGION_ALIAS("DDR", m_ddr);
REGION_ALIAS("UNCACHED_DDR", m_uncached_ddr);

#include "common/plo-arm-hivecs.lds"

/* Additional sections */
SECTIONS
{
	.ddr (NOLOAD) :
	{
		. = ALIGN(4);
		__ddr_start = .;
		*(.ddr*)
		__ddr_end = .;
	} > DDR

	.uncached_ddr (NOLOAD) :
	{
		. = ALIGN(4);
		__uncached_ddr_start = .;
		*(.uncached_ddr*)
		__uncached_ddr_end = .;
	} > UNCACHED_DDR
}


#endif /* end of __LINKER__ */


#endif /* end of ARMV7R5F_ZYNQMP_LDT */
