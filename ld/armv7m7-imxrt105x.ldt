/*
 * Phoenix-RTOS
 *
 * Operating system loader
 *
 * Linker Template and Platform Config for ARMv7 M7 i.MX RT105x
 *
 * Copyright 2021-2022 Phoenix Systems
 * Author: Gerard Swiderski
 *
 * This file is part of Phoenix-RTOS.
 *
 * %LICENSE%
 */


#ifndef ARMV7M7_IMXRT105X_LDT
#define ARMV7M7_IMXRT105X_LDT


/* Platform specific definitions */
#define SIZE_PAGE  0x200
#define SIZE_STACK (4 * SIZE_PAGE)
#define SIZE_HEAP  (8 * SIZE_PAGE)

#if defined(__LINKER__)

/* FlexRAM configuration */
FLEXRAM_CONFIG = 0xaaaabff5;

/* Memory map setup */
MEMORY
{
	m_itcm    (rwx) : ORIGIN = 0x00000000, LENGTH = 5 * 32k
	m_dtcm    (rw)  : ORIGIN = 0x20000000, LENGTH = 9 * 32k
	m_ocram   (rwx) : ORIGIN = 0x20200000, LENGTH = 2 * 32k
	m_flash   (rx)  : ORIGIN = 0x70000000, LENGTH = 4M
}

#if defined (ram)

/* RAM image */
REGION_ALIAS("PLO_IMAGE", m_itcm);
REGION_ALIAS("DATA", m_itcm);

#else

/* FLASH image */
REGION_ALIAS("PLO_IMAGE", m_flash);
REGION_ALIAS("DATA", m_dtcm);

#endif

/* RAM and FLASH image */
REGION_ALIAS("TCM_TEXT", m_itcm);
REGION_ALIAS("BSS", m_dtcm);
REGION_ALIAS("HEAP", m_dtcm);
REGION_ALIAS("STACK", m_dtcm);


#include "common/plo-arm.lds"

#endif /* end of __LINKER__ */


#endif /* end of ARMV7M7_IMXRT105X_LDT */
